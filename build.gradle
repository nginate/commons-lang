buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
    }
}

plugins {
    id 'nebula.optional-base' version '3.1.0'
    id "com.jfrog.bintray" version "1.7.1"
    id 'net.researchgate.release' version '2.4.0'
}

description = 'Misc utilities to hide most common boilerplate code. Requires jdk8 at least'

ext {
    projectUrl = 'https://github.com/nginate/commons-lang'
    vcsUrl = 'https://github.com/nginate/commons-lang.git'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: 'nebula.optional-base'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "info.solidsoft.pitest"
apply from: 'gradle/distribution.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Utils
    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.4'

    // Annotation processors
    compile 'org.projectlombok:lombok:1.16.8', optional
    compile 'com.google.code.findbugs:jsr305:3.0.1', optional

    testCompile 'junit:junit:4.12'
    testCompile 'org.testng:testng:6.9.12'
    testCompile 'org.assertj:assertj-core:3.5.1'
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    mutateStaticInits = true
    timestampedReports = false
}

jacocoTestReport {
    group = "report"
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

build.dependsOn install
